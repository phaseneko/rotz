10 CLEAR300,60850!:MAXFILES=1:EA=60850!:TA=60950!:IA=61430!:MA= 61930!:CLS:SCREEN0,0:Z$="":X$="":C$=""
20 '60850-60949: ENEMY DATA
30 '60950-61429: TILE DATA
40 '61430-61929: ITEM DATA
50 '61930-62330: MAP DATA
60 PRINT"Retro of the Zombies Ver0.1"
70 PRINT"Ne^3 Games 2022"
80 PRINTSTRING$(39,"-")
90 'LOAD ITEM DATAS (MAX 50)
100 BLOAD"ITEM"
110 OPEN"ITNAM"FORINPUTAS#1
120 DIMIM$(50):IC=0:IM$(0)="FIST"
130 INPUT#1,IM$(IC+1)
140 IFNOTEOF(1)THENIC=IC+1:GOTO130
150 CLOSE
160 PRINTIC+1;"items loaded."
170 'LOAD TILE DATAS (MAX 20)
180 BLOAD"TILE"
190 OPEN"TLNAM"FORINPUTAS#1
200 DIMTM$(20):TC=0
210 INPUT#1,TM$(TC)
220 IFNOTEOF(1)THENTC=TC+1:GOTO210
230 CLOSE
240 PRINTTC+1;"tiles loaded."
250 BLOAD"ENEMY"
260 OPEN"ENNAM"FORINPUTAS#1
270 DIMEM$(5):EC=0
280 INPUT#1,EM$(EC)
290 IFNOTEOF(1)THENEC=EC+1:GOTO280
300 CLOSE
310 PRINTEC+1;"enemies loaded."
320 INPUT"Map Name E.g. TOWN1";SS$
340 ONERRORGOTO350:BLOADSS$:GOTO360
350 PRINT"Map not found!":RESUME320
360 W=20:H=20:SE=VAL(LEFT$(TIME$,2))+VAL(MID$(TIME$,4,2))+VAL( RIGHT$(TIME$,2))
370 CLOSE:PRINTCHR$(27);"V"
380 'INIT GAME
390 ONERRORGOTO0
400 TS$=".+-|/^SGD*H#MPCR$%!?"
410 WS$="-\|/":MP=12:HP=20:FP=10
420 DIMVC(4):DIMVR(8)
430 DATA3,7,12,16,0,6,4,10,9,15,13,19
440 RESTORE430:FORI=0TO3:READVC(I):NEXT:FORI=0TO7:READVR(I):NEXT
450 DIMIB(34):ST%=0:EM=1
460 'RANDOMIZE SPAWN POINT
470 GOSUB3200
471 XX=INT(XX/3):YY=INT(YY/3)
472 IFRND(SE)<.5THENXX=W-1-XX
473 IFRND(SE)<.5THENYY=H-1-YY
474 CX=XX:CY=YY:VX=CX:VY=CY:IFPEEK(MA+CY*W+CX)MOD50=4THEN470'AVOID WATER
475 HX=W-1-CX:HY=H-1-CY:POKEMA+HY*W+HX,68:IFABS(HX-W/2)>ABS(HY-H/2)GOTO476ELSEGOTO478
476 AY=H/2:IFHX>W/2THENAX=0ELSEAX=W-1
477 GOTO480
478 AX=W/2:IFHY>H/2THENAY=0ELSEAY=H-1
480 ZZ=H:FORI=0TO3:YY=ABS(CY-VC(I)):IFYY<ZZTHENZZ=YY:VP=I:SL=VR(I*2):NEXT
481 XX=3:YY=8:GOSUB4040
490 'ENTER GAME
500 FORSY=0TO6:LOCATE20,SY:PRINT":":NEXT
510 GOSUB550'DRAW UI
520 FD=1:GOSUB830'DRAW MAP FULL
530 GOSUB1080'WAIT
540 END
550 'SUB DRAW UI
551 GOSUB2090
560 FORSY=0TO7:LOCATE21,SY:PRINTSTRING$(19," "):NEXT
570 LOCATE21,0:PRINT"Move: ";STRING$(MP,Z$);STRING$(12-MP,".")
580 LOCATE21,1:PRINT"Cost:":IFVM=0THENLOCATE24,2:PRINTSTRING$(10,"- ")
590 IFVMGOTO700
600 IFCY=0THENGOTO620ELSEX=CX:Y=CY-1:SS$=STR$(ET+PEEK(TA+(PEEK(MA+ Y*W+X)MOD50)*24)):SS$=RIGHT$(SS$,LEN(SS$)-1)
610 LOCATE29,1:PRINT"N:";SS$
620 IFCY=19THENGOTO640ELSEX=CX:Y=CY+1:SS$=STR$(ET+PEEK(TA+(PEEK(MA+ Y*W+X)MOD50)*24)):SS$=RIGHT$(SS$,LEN(SS$)-1)
630 LOCATE29,3:PRINT"S:";SS$
640 IFCX=0THENGOTO660ELSEX=CX-1:Y=CY:SS$=STR$(ET+PEEK(TA+(PEEK(MA+ Y*W+X)MOD50)*24)):SS$=RIGHT$(SS$,LEN(SS$)-1)
650 LOCATE24,2:PRINT"W:";SS$
660 IFCX=19THENGOTO680ELSEX=CX+1:Y=CY:SS$=STR$(ET+PEEK(TA+(PEEK(MA+ Y*W+X)MOD50)*24)):SS$=RIGHT$(SS$,LEN(SS$)-1)
670 LOCATE34,2:PRINT"E:";SS$
680 X=CX:Y=CY:SS$=STR$(ET+PEEK(TA+(PEEK(MA+Y*W+X)MOD50)*24+1)):SS$= RIGHT$(SS$,LEN(SS$)-1)
690 LOCATE29,2:PRINT"@:";SS$:GOTO740
700 X=VX:Y=VY:SS$=STR$(PEEK(TA+(PEEK(MA+Y*W+X)MOD50)*24)):SS$= RIGHT$(SS$,LEN(SS$)-1)
710 LOCATE27,1:PRINT"Travel:";SS$
720 SS$=STR$(PEEK(TA+(PEEK(MA+Y*W+X)MOD50)*24+1)):SS$=RIGHT$(SS$, LEN(SS$)-1)
730 LOCATE27,2:PRINT"Search:";SS$
740 LOCATE21,4:PRINT"HP:"
741 LOCATE26,4:EXEC17058:PRINTSTRING$(INT(HP/2)," "):EXEC17063
742 SS$=STR$(HP):SS$=RIGHT$(SS$,LEN(SS$)-1):LOCATE26+INT(HP/2), 4:PRINTSTRING$(HPMOD2,C$);STRING$(INT((20-HP)/2),".");" ";STRING$(2-LEN(SS$)," ");SS$
745 LOCATE21,5:PRINT"Stat:";STRING$(7,"="):SS$=STR$(PP):SS$=RIGHT$(SS$,LEN(SS$)-1):LOCATE34,5:PRINT"PP:";STRING$(2-LEN(SS$)," ");SS$
750 IFST%AND1THENLOCATE27,5:PRINT"B"
760 IFST%AND2THENLOCATE28,5:PRINT"H"
770 IFST%AND4THENLOCATE29,5:PRINT"T"
780 IFST%AND8THENLOCATE30,5:PRINT"W"
790 IFST%AND16THENLOCATE31,5:PRINT"C"
791 IFST%AND32THENLOCATE32,5:PRINT"I"
792 IFST%AND64THENLOCATE33,5:PRINT"S"
800 LOCATE21,6:PRINT"[I]tem  [S]earch"
810 LOCATE29,7:PRINT"[Q]uest [R]"
811 IFVM>0THENEXEC17058
812 LOCATE21,7:PRINT"[V]iew":EXEC17063
813 IFST%AND4THENLOCATE29,7:PRINT"[R]est  [Q]"
820 RETURN'DRAW UI
830 'SUB DRAW MAP
840 VD=2:XX=1:YY=1
850 IFFDTHENFX=0:LX=W-1:FY=SL:LY=SL+6:GOTO900
860 FX=CX-VD+1:FY=CY-VD+1:LX=CX+VD-1:LY=CY+VD-1
870 IFOX<0THENLX=FXELSEIFOX>0THENFX=LX
880 IFOY<0THENLY=FYELSEIFOY>0THENFY=LY
890 GOTO930
900 IFCX>10THENFX=W-1:LX=0:XX=-1
910 IFCY>SL+3THENFY=SL+6:LY=SL:YY=-1
920 FORI=0TO6:LOCATE20,I:PRINT":":NEXT
921 EXEC17058:LOCATE20,INT(CY*7/H):PRINT"@":LOCATE20,INT(HY*7/H):PRINT"!":IFQP%AND1THENLOCATE20,INT(AY*7/H):PRINT"?"
922 FORI=0TO6:LOCATE0,I:PRINTSTRING$( 20," "):NEXT:EXEC17063:LOCATE5,3:SS$="Loading...":GOSUB1460
930 FORY=FYTOLYSTEPYY:FORX=FXTOLXSTEPXX
940 IFFDTHENGOTO960
950 IFX<0ORY<0ORX>=WORY>=HTHEN1040
960 SY=Y-SL:TL=PEEK(MA+Y*W+X)
970 IFFD=0ANDSY<0ORSY>6THEN1040
980 OX=ABS(X-CX):OY=ABS(Y-CY)
990 IFTL<50ANDOX<VDANDOY<VDTHENTL=TL+50:POKEMA+Y*W+X,TL'DISCOVERED
1000 LOCATEX,SY:IFFDGOTO1030
1010 PRINTMID$(TS$,TLMOD50+1,1)
1020 GOTO1040
1030 IFTL>=50THENPRINTMID$(TS$,TLMOD50+1,1)ELSEPRINTC$
1040 NEXTX:NEXTY
1050 TL=PEEK(MA+VY*W+VX):SS$=TM$(TLMOD50):IFTL>=200THENSS$=SS$+" (Empty)"
1060 GOSUB1460:FD=0
1070 RETURN'DRAW MAP
1080 'SUB WAIT
1090 WT=WT+.5:LOCATE0,7:PRINTMID$(WS$,(WTMOD4)+1,1)
1100 X=VX:Y=VY:LOCATEX,Y-SL
1110 IFVM>0THENSS$="V"ELSESS$="@"
1120 IF(WTMOD4)<2THENPRINTSS$ELSETL=PEEK(MA+Y*W+X)MOD50:PRINTMID$( TS$,TL+1,1)
1130 KY$=INKEY$:IFKY$=""THEN1080
1131 AA=ASC(KY$):IFAA>=97ANDAA<=122THENAA=AA-32:KY$=CHR$(AA)
1140 IFKY$="V"ORKY$=CHR$(27)ANDVMTHENGOSUB1240
1150 IFKY$="H"ORKY$=CHR$(29)THENOX=-1:OY=0:GOSUB1300
1160 IFKY$="L"ORKY$=CHR$(28)THENOX=1:OY=0:GOSUB1300
1170 IFKY$="K"ORKY$=CHR$(30)THENOX=0:OY=-1:GOSUB1300
1180 IFKY$="J"ORKY$=CHR$(31)THENOX=0:OY=1:GOSUB1300
1190 IFKY$="\"THENGOSUB1560:GOSUB550
1200 IFKY$="I"THENKY$="":GOSUB1790:GOSUB2830:GOSUB550
1210 IFKY$="S"THENGOSUB1950
1220 IFKY$="C"THENGOSUB2150
1221 IFKY$="R"THENGOSUB2910:GOSUB550
1222 IFKY$="Q"THENGOSUB3040
1230 GOTO1080'LOOP
1240 'SUB VIEW
1250 VX=CX:VY=CY
1260 IFVM>0ANDVM<>VP+1THENVP=VM-1:SL=VR(VP*2):FD=1:GOSUB830
1270 IFVM=0THENVM=VP+1ELSEVM=0
1280 GOSUB550
1290 RETURN'VIEW
1300 'SUB MOVE
1310 X=VX+OX:Y=VY+OY
1320 IFX<0ORY<0ORX>=WORY>=HTHENRETURN
1330 TL=PEEK(MA+Y*W+X)
1340 IFVM=0ANDMP<ET+PEEK(TA+(TLMOD50)*24)THENSS$="Need to rest.":GOSUB1460:RETURN
1350 IFVM>0ANDTL<50THENRETURN
1360 IFVM=0THENMP=MP-PEEK(TA+(TLMOD50)*24)-ET
1370 GOSUB1500
1380 VX=X:VY=Y:IFVM=0THENCX=VX:CY=VY
1390 FD=0
1400 IFY>0ANDY<=VR(VP*2)THENVP=VP-1:SL=VR(VP*2):FD=1
1410 IFY<H-1ANDY>=VR(VP*2+1)THENVP=VP+1:SL=VR(VP*2):FD=1
1420 GOSUB830'DRAW MAP
1421 IFVM=0AND(ST%AND32)>0THENHP=HP-1:IFHP>0THENSS$="You feel sick.":GOSUB1460ELSESS$="You become one of'em":GOSUB1460:DC%=DC%OR4:GOTO10000
1430 GOSUB550'DRAW UI
1431 IF(QP%AND1)=0ANDCX=HXANDCY=HYTHENEX=3:EC=1:EH=PEEK(EA+EX*20+6):SY=0:CLS:GOSUB2180:GOSUB2390:IFCX=HXTHENGOSUB4140'BOSS1
1432 IF(QP%AND1)ANDCX=AXANDCY=AYTHENEX=4:EC=1:EH=PEEK(EA+EX*20+6):SY=0:CLS:GOSUB2180:GOSUB2390:IFCX=AXTHENGOSUB4200'BOSS2
1440 IFVM=0THENGOSUB2230'ENCOUNTER
1450 RETURN'MOVE
1460 'SUB DRAW MSG
1461 IFSS$=""THENRETURN
1470 LOCATE0,7:PRINT"!";STRING$(20," "):LOCATE1,7:PRINTSS$
1480 SS$=STR$(SD):ZZ=LEN(SS$):LOCATE20-ZZ,7:PRINT"#";MID$(SS$,2,ZZ-1):SS$=""
1490 RETURN'DRAW MSG
1500 'SUB PRE MOVE
1510 IFX<0ORY<0ORX>=WORY>=HTHENRETURN
1520 LOCATEX,Y-SL:IFVM>0THENPRINT"V"ELSEPRINT"@"
1530 LOCATEVX,VY-SL
1540 TL=PEEK(MA+VY*W+VX)MOD50:PRINTMID$(TS$,TL+1,1):IFVM=0THENSP=0:GOSUB2090
1550 RETURN'PRE MOVE
1560 'SUB CMD
1570 LOCATE0,7:EXEC17026:INPUTSS$,XX,YY
1580 IFSS$="MP"THENMP=XX
1590 IFSS$="HP"THENHP=XX
1600 IFSS$="ADD"THENIX=XX:IC=YY:GOSUB1630
1601 IFSS$="RM"THENIX=XX:IC=YY:GOSUB1710
1610 IFSS$="SD"THENSD=XX
1620 RETURN
1630 'SUB ADD ITEM
1640 IFWP+PEEK(IA+IX*8+3)*IC>=100THENSS$="Overburdened.":GOSUB1460:RETURN
1650 FORI=0TO16
1660 IFIB(I*2)=0THENIB(I*2)=IX:IB(I*2+1)=IC:GOTO1690
1670 IFIB(I*2)=IXTHENIB(I*2+1)=IB(I*2+1)+IC:GOTO1690
1680 NEXT
1690 WP=WP+PEEK(IA+IX*8+3)*IC:SC=SC+PEEK(IA+IX*8+6)+PEEK(IA+IX*8+7)
1700 RETURN'ADD ITEM
1710 'SUB RM ITEM
1720 FORI=0TO15
1730 IFIB(I*2)<>IXTHEN1770
1740 IB(I*2+1)=IB(I*2+1)-IC:WP=WP-PEEK(IA+IX*8+3)*IC
1750 IFIB(I*2+1)<=0THENIB(I*2)=0ELSE1770
1760 FORJ=ITO14:IB(J*2)=IB(J*2+2):IB(J*2+1)=IB(J*2+3):NEXTJ
1770 NEXTI
1780 RETURN'RM ITEM
1790 ID=0'SUB ITEM MENU
1800 ZZ=0:LOCATE21,0:PRINTSTRING$(19,"-"):LOCATE22,0:PRINT"[Item]" :LOCATE32,0:SS$=STR$(WP):SS$=RIGHT$(SS$,LEN(SS$)-1):PRINT"<Wt:" ;SS$;">"
1810 FORI=1TO7:LOCATE21,I:PRINTSPACE$(19):NEXT
1820 FORI=0TO6
1830 IFZZ*7+I>=16THEN1860ELSELOCATE21,I+1
1840 IFIB(ZZ*14+I*2)=0ORIB(ZZ*14+I*2+1)=0THENPRINT"[";CHR$(65+ZZ*7+ I);"] N/A":GOTO1860
1850 PRINT"[";CHR$(65+ZZ*7+I);"] ";IM$(IB(ZZ*14+I*2));" x";IB(ZZ*14+I*2+1)
1860 NEXT
1861 LOCATE35,6:IFIDTHENEXEC17058
1862 PRINT"[DEL]":EXEC17063
1863 LOCATE35,7:PRINT"[ESC]"
1870 WT=WT+.5:LOCATE0,7:PRINTMID$(WS$,(WTMOD4)+1,1):IFWT>=100THENWT=0
1880 KY$=INKEY$:IFKY$=""THEN1870
1881 AA=ASC(KY$):IFAA>=97ANDAA<=122THENAA=AA-32:KY$=CHR$(AA)
1890 IFKY$=CHR$(27)THENIX=-1:RETURN
1900 IF(KY$=CHR$(30)ORKY$=CHR$(29))ANDZZ>0THENZZ=ZZ-1:GOTO1810
1910 IF(KY$=CHR$(31)ORKY$=CHR$(28))ANDZZ<2THENZZ=ZZ+1:GOTO1810
1911 IFKY$=CHR$(127)THENID=NOTID:GOTO1861
1920 XX=ASC(KY$):IFXX<ASC("A")ORXX>ASC("P")THEN1870
1930 IFIB((XX-ASC("A"))*2)>0THENIX=IB((XX-ASC("A"))*2):IFIDTHENIC=1:GOSUB1710:GOTO1800ELSEKY$="":RETURN
1940 GOTO1870
1950 'SUB SEARCH
1960 IFVMTHENRETURN
1970 X=CX:Y=CY:TL=PEEK(MA+Y*W+X)
1980 IFTL>=200THENSS$="Nothing's left.":GOSUB1460:RETURN
1990 ZZ=PEEK(TA+(TLMOD50)*24+1):IFMP<ET+ZZTHENSS$="Need to rest.":GOSUB1460:RETURN
2000 IFWP>=100THENSS$="Overburdened.":GOSUB1460:RETURN
2010 MP=MP-ZZ-ET:XX=PEEK(TA+(TLMOD50)*24+3):YY=0:FORI=0TOXX-1:YY=YY+PEEK(TA+(TLMOD50)*24+5+I*2):NEXT
2020 ZZ=RND(SEED)*YY
2030 YY=0:IX=0:IC=1:FORI=0TOXX-1:YY=YY+PEEK(TA+(TLMOD50)*24+5+I*2) :IFYY>=ZZTHENIX=PEEK(TA+(TLMOD50)*24+4+I*2):ZZ=0:GOTO2050
2040 NEXT
2050 IFIX>0THENSS$="Found "+IM$(IX):GOSUB1630:GOSUB1460ELSESS$="Nope. Try again.":GOSUB1460
2060 IFRND(SEED)<.5THENTL=TL+50:POKEMA+CY*W+CX,TL
2070 GOSUB550:GOSUB2230
2080 RETURN'SEARCH
2090 'SUB STATUS
2091 IFST%AND1THENXX=1ELSEXX=0
2100 IFWP>80THENST%=ST%OR1ELSEST%=ST%ANDNOT1'B
2101 IF(WP>80)AND(XX=0)THENET=ET+1
2102 IF(WP<=80)AND(XX=1)THENET=ET-1
2110 IFFP<5THENST%=ST%OR2ELSEST%=ST%ANDNOT2'H
2120 IFMP<4THENST%=ST%OR4ELSEST%=ST%ANDNOT4'T
2130 IFHP<5THENST%=ST%OR8ELSEST%=ST%ANDNOT8'W
2131 IFSP>10THENST%=ST%OR64ELSEST%=ST%ANDNOT64'S
2140 RETURN'STATUS
2150 'SUB CAREFUL MODE
2160 IFST%AND16THENST%=ST%ANDNOT16:ET=ET-1:EM=1ELSEST%=ST%OR16:ET= ET+1:EM=.5
2170 GOSUB550:RETURN'SAFE MODE
2180 'SUB DRAW COMBAT UI
2181 LOCATE0,0:PRINTSTRING$(40,"-")
2182 LOCATE1,0:PRINT"[Combat]":LOCATE11,0:PRINT"<HP:":LOCATE15, 0:EXEC17058:PRINTSTRING$(INT(HP/2)," "):EXEC17063
2183 LOCATE15+INT(HP/2),0:PRINTSTRING$(HPMOD2,C$);STRING$(INT((20-HP)/2),".");">":SS$=STR$(PP):SS$=RIGHT$(SS$,LEN(SS$)-1):LOCATE28,0:PRINT"<PP:";SS$;">"
2184 FORI=1TO7:LOCATE21,I:PRINTSPACE$(19):NEXT
2185 FORI=0TOEC-1:LOCATE21,1+I*2:PRINTEM$(EX);I:NEXT
2186 ZZ=PEEK(EA+EX*20+6):EXEC17058:FORI=0TOEC-2:LOCATE21,2+I* 2:PRINTSTRING$(INT(ZZ/2)," "):NEXT:EXEC17063
2187 FORI=0TOEC-2:LOCATE21+INT(ZZ/2),2+I*2:PRINTSTRING$(ZZMOD2,C$); STRING$(INT((20-ZZ)/2),"."):NEXT
2188 ZZ=EH:EXEC17058:I=EC-1:LOCATE21,2+I*2:PRINTSTRING$(INT(ZZ/2)," "):EXEC17063
2189 LOCATE21+INT(ZZ/2),2+I*2:PRINTSTRING$(ZZMOD2,C$);STRING$(INT(( 20-ZZ)/2),".")
2220 RETURN'DRAW COMBAT UI
2230 'SUB ENCOUNTER
2231 EO=EO+1:IFEO<3OR(ST%AND64)THENRETURNELSEEO=0
2233 IFSD<2THENRETURN
2240 X=CX:Y=CY:TL=PEEK(MA+Y*W+X)MOD50
2250 ER=PEEK(TA+TL*24+2)*EM
2260 IFRND(SEED)*20>ERTHENRETURN
2270 SS$="Encounter!!":GOSUB1460
2280 WT=WT+.5:LOCATE0,7:PRINTMID$(WS$,(WTMOD4)+1,1):IFWT>=100THENWT=0
2290 KY$=INKEY$:IFKY$=""THEN2280
2300 CLS:FORI=1TO7:LOCATE20,I:PRINT":":NEXT
2310 'RAND ENEMY
2320 YY=0:FORI=0TO4:IFSD>=PEEK(EA+I*20+1)THENYY=YY+PEEK(EA+I*20) :NEXT
2330 ZZ=RND(SEED)*YY
2340 YY=0:EX=0:FORI=0TO4
2350 IFSD>=PEEK(EA+I*20+1)THENYY=YY+PEEK(EA+I*20)
2360 IFYY>=ZZTHENEX=I:ZZ=0:GOTO2380
2370 NEXT
2380 XX=PEEK(EA+EX*20+7):YY=PEEK(EA+EX*20+8):EC=INT(XX+RND(SEED)*( YY-XX)+.5):EH=PEEK(EA+EX*20+6):SY=0
2390 GOSUB2180:GOSUB2510:PRINT"Enemy:";EM$(EX);" x";EC
2401 GOSUB2510:PRINT"[ESC]ape or [F]ight?
2410 GOSUB2470'WAIT KEY
2411 IFKY$<>CHR$(27)ANDKY$<>"\"ANDKY$<>"F"THEN2410
2420 IFKY$=CHR$(27)THENGOSUB2770:IFHC=0THENFD=1:LOCATE20,0:PRINT":" :GOSUB550:GOSUB830:RETURNELSEGOTO2390
2430 IFKY$="F"THENGOSUB2540
2431 IFKY$="\"THENGOSUB1560:GOSUB2180
2432 IFEC=0THENGOSUB2510:PRINT"All Clear! -MORE-":GOSUB2470:FD=1:LOCATE20,0:PRINT":" :GOSUB550:GOSUB830:RETURN
2433 GOSUB2690
2450 GOTO2390
2460 RETURN'ENCOUNTER
2470 'SUB WAIT KEY
2480 WT=WT+.5:LOCATE39,7:PRINTMID$(WS$,(WTMOD4)+1,1):IFWT>=100THENWT=0
2490 KY$=INKEY$:IFKY$=""THEN2480
2491 AA=ASC(KY$):IFAA>=97ANDAA<=122THENAA=AA-32:KY$=CHR$(AA)
2500 RETURN'WAIT KEY
2510 'SUB NEW LINE
2520 SY=SY+1:IFSY>7THENSY=1
2521 YY=SY+1:IFYY>7THENYY=1
2522 LOCATE0,SY:PRINTSPACE$(20):LOCATE0,YY:PRINTSTRING$(20,"^")
2530 LOCATE0,SY:RETURN'NEW LINE
2540 'SUB FIGHT
2550 GOSUB2510:PRINT"Choose weapon...":GOSUB1790:GOSUB2180:AT=0
2560 IFIX<0THEN2610
2561 IFRIGHT$(IM$(IX),1)<>"G"THEN2570
2562 XX=IX:YY=IC:ZZ=0:FORI=0TO15:IFIB(I*2)=(IX+1)THENIX=IX+1:IC= 1:ZZ=1:GOTO2564
2563 NEXT
2564 IFZZTHENGOSUB1710:IX=XX:IC=YYELSEGOSUB2510:PRINT"No Ammo!":IX=XX:IC=YY:AT=1:FC=5:GOTO2640
2570 XX=PEEK(IA+IX*8+4):YY=PEEK(IA+IX*8+5):FC=PEEK(IA+IX*8+1)
2580 IFXX=1THENAT=PEEK(IA+IX*8+6)ELSEIFYY=1THENAT=PEEK(IA+IX*8+7) ELSEAT=INT(PEEK(IA+IX*8+3)/5)+1:FC=5+AT
2600 GOTO2640
2610 GOSUB2510:PRINT"Fight with [F]ist?":GOSUB2470
2620 IFKY$="F"THENAT=1:FC=5:GOTO2640ELSERETURN
2640 IFIX<0THENIX=0
2641 GOSUB2510:PRINT"Attack with ";IM$(IX)
2650 GOSUB2510:IFRND(SEED)*20>FCTHEN2660ELSEPRINT"Miss!":RETURN
2660 XX=INT(AT/2):YY=AT:ZZ=INT(RND(SEED)*(YY-XX))+XX+1:IFRND(SEED)<= .05THENZZ=ZZ*2
2661 EH=EH-ZZ:IFEH<=0THENPRINT"Kill!";ZZ;"damage!":KC=KC+1:SC=SC+PEEK(EA+EX*20+6)ELSEPRINT"Hit!"; ZZ;"damage!"
2662 'BRK WEP
2663 ZZ=PEEK(IA+IX*8):IFRND(SEED)*20<ZZTHENGOSUB2510:PRINTIM$(IX);" breaks.":IC=1:GOSUB1710
2664 IFEH>0THENRETURN
2670 EH=PEEK(EA+EX*20+6):EC=EC-1:XX=PEEK(EA+EX*20+9):IFXX=0THEN2676
2671 YY=0:FORI=0TOXX-1:YY=YY+PEEK(EA+EX*20+11+I*2):NEXT
2672 ZZ=RND(SEED)*YY
2673 YY=0:IX=0:IC=1:FORI=0TOXX-1:YY=YY+PEEK(EA+EX*20+11+I*2):IFYY> ZZTHENIX=PEEK(EA+EX*20+10+I*2):ZZ=0:GOTO2675
2674 NEXT
2675 IFIX>0THENGOSUB2510:PRINT"Found ";IM$(IX):GOSUB1630
2676 IFEC=0THENRETURNELSEGOSUB2180
2680 RETURN 'FIGHT
2690 'SUB ENEMY ATTACK
2700 FORI=0TO31:LOCATE39,7:PRINTMID$(WS$,IMOD4+1,1):NEXT:HC=0:XX=PEEK(EA+EX*20+2):YY=PEEK(EA+EX*20+3)
2710 FORI=0TOEC-1
2711 ZZ=INT(RND(SEED)*(YY-XX))+XX+1
2720 GOSUB2510:PRINTEM$(EX);" attacks!"
2730 FC=PEEK(EA+EX*20+5):GOSUB2510:IFRND(SEED)*20>FCTHEN2731ELSEPRINT"Miss!":GOTO2750
2731 FC=RND(SE)*PP/40+PP/40!:PP=PP-INT(ZZ*FC)-1:IFPP<0THENPP=0
2740 ZZ=INT(ZZ*(1-FC)):HP=HP-ZZ:IFHP<=0THEN10000ELSEPRINT"Hit!";ZZ;"damage!":HC=HC+1
2741 IFRND(SE)*20<PEEK(EA+EX*20+4)THENST%=ST%OR32:GOSUB2510:PRINT"Infected!"
2750 NEXT
2760 RETURN 'ENEMY ATTACK
2770 'SUB ESCAPE
2780 GOSUB2510:PRINT"Try to escape..."
2790 GOSUB2690:GOSUB2510:IFRND(SE)*2<HCTHENPRINT"Failed!":RETURN
2791 IFCX>W/2THENCX=CX-1ELSECX=CX+1
2800 VX=CX:HC=0:ZZ=INT(RND(SE)*16):IX=IB(ZZ*2):IFIX=0THENPRINT"You escaped. -MORE-":GOSUB2470:RETURN
2810 IC=1:GOSUB1710:PRINT"Escaped in a hurry...":GOSUB2510:PRINTIM$(IX);" lost! -MORE-":GOSUB2470
2820 RETURN'ESCAPE
2830 'SUB USE ITEM
2840 IFIX<=0THENRETURN
2850 XX=PEEK(IA+IX*8+4):YY=PEEK(IA+IX*8+5)
2851 IFXX=2THENZZ=PEEK(IA+IX*8+6):GOSUB2870
2852 IFYY=2THENZZ=PEEK(IA+IX*8+7):GOSUB2870
2853 IFXX=3THENZZ=PEEK(IA+IX*8+6):GOSUB2900
2854 IFYY=3THENZZ=PEEK(IA+IX*8+7):GOSUB2900
2855 IFXX=6THENZZ=PEEK(IA+IX*8+6):GOSUB2980
2856 IFXX=8THENZZ=PEEK(IA+IX*8+6):GOSUB4000
2857 IFRND(SE)*20<PEEK(IA+IX*8)THENIC=1:GOSUB1710:SS$=IM$(IX)+" used up!":GOSUB1460
2860 RETURN'USE ITEM
2870 'SUB FOOD
2880 FP=FP+ZZ:SS$="Used "+IM$(IX):GOSUB1460
2881 IFRND(SE)*20<PEEK(IA+IX*8+2)THENSS$="It's rotten!":GOSUB1460:ST%=ST%OR32:CP=0
2882 RETURN'FOOD
2890 'SUB MEDICINE
2900 CP=CP+ZZ:SS$="Used "+IM$(IX):GOSUB1460:RETURN'MEDICINE
2910 'SUB REST
2912 TL=PEEK(MA+CY*W+CX)MOD50:IFTL>=4THEN2919
2913 SS$="Bad place to rest.":MP=MP+6:FP=0:CP=0:SD=SD+1:IFMP>12THENMP=12
2914 GOSUB1460:GOTO2950
2919 ZZ=RND(SE):IFZZ<.5THENZZ=.5ELSEZZ=1
2920 HP=HP+INT(FP*ZZ):FP=0:IFHP>20THENHP=20
2930 IFRND(SE)*ZZ*20<CP+2THENST%=ST%ANDNOT32
2940 CP=0:SD=SD+1:MP=MP+INT(12*ZZ):IFMP>12THENMP=12
2941 IFZZ>.9THENSS$="You slept well."ELSESS$="You barely slept."
2950 IFST%AND2THENHP=HP-3:IFHP>0THENSS$="You starve."ELSESS$="You starve to death":DC%=DC%OR1:GOTO10000
2970 GOSUB1460:GOSUB2230:RETURN'REST
2980 'SUB SAFE HOUSE
2981 IFPEEK(MA+CY*W+CX)MOD50<4THENRETURN
2990 XX=ZZ:IFST%AND64THENRETURN
3000 SS$="Fortifying house.":GOSUB1460
3010 SP=SP+INT(RND(SE)*XX*.5)+XX/2:GOSUB2090
3020 IFST%AND64THENSS$="Safehouse built.":GOSUB1460
3030 RETURN'SAFE HOUSE
3040 'SUB QUEST
3050 ZZ=SC*2+SD*5:CLS:LOCATE0,0:PRINTSTRING$(40,"-"):LOCATE1,0:PRINT"[Quest]":LOCATE22,0:PRINT"[Menu]":FORI=0TO7:LOCATE20,I:PRINT":":NEXT
3051 IFST%AND16THENEXEC17058
3052 LOCATE22,2:PRINT"[C]areful Mode":EXEC17063
3053 LOCATE22,4:PRINT"[R]estart":LOCATE22,5:PRINT"E[X]it  [ESC]":LOCATE22,7:PRINT"Ne^3 Games 2022"
3060 IFQP%AND1THENEXEC17058:ZZ=ZZ+200
3070 LOCATE0,1:PRINT"1> Reach Home":EXEC17063
3080 IFQP%AND2THENEXEC17058:ZZ=ZZ+200
3090 LOCATE0,2:PRINT"2> Emily Lives":EXEC17063
3100 IFQP%AND4THENEXEC17058:ZZ=ZZ+200
3110 LOCATE0,3:PRINT"3> Reach Camp":EXEC17063
3120 IFKC>=40THENEXEC17058:ZZ=ZZ+200
3130 LOCATE0,4:PRINT"X>";KC;"/ 40 kills":EXEC17063
3140 CS=0:FORI=0TO15:IFIB(I*2)=20THENCS=IB(I*2+1):GOTO3150
3141 NEXT
3150 IFCS>=10THENEXEC17058:ZZ=ZZ+200
3160 LOCATE0,5:PRINT"Y>";CS;"/ 10 $10K$":EXEC17063
3161 SS$=STR$(SD):SS$=RIGHT$(SS$,LEN(SS$)-1):LOCATE0,6:PRINT"Survived ";SS$;" day"
3162 SS$=STR$(ZZ):SS$=RIGHT$(SS$,LEN(SS$)-1):LOCATE0,7:PRINT"Score: ";SS$;"00"
3170 GOSUB2470
3171 IFKY$=CHR$(27)THENIFHP<=0THEN10ELSEFORI=0TO6:LOCATE20,I:PRINT":":NEXT:FD=1:GOSUB550:GOSUB830:RETURN
3172 IFKY$="C"THENGOSUB2150
3173 IFKY$="R"GOTO10
3174 IFKY$="X"THENMENU
3190 GOTO3170
3200 'SUB RAND XX YY
3210 XX=INT(RND(SE)*W):YY=INT(RND(SE)*H):RETURN'RAND XX YY
4000 'SUB PROTECT
4010 PP=PP+ZZ:IFPP>20THENPP=20
4020 SS$="Wearing "+IM$(IX):GOSUB1460
4030 RETURN'PROTECT
4040 'SUB INTRO
4050 CLS:ONERRORGOTO4060:GOTO4070
4060 RESUME4110
4070 OPEN"INTRO.DO"FORINPUTAS#1
4080 FORI=0TOYY:SS$="":INPUT#1,SS$:IFI<XXTHEN4100
4090 PRINTSPACE$(20-LEN(SS$)/2);SS$
4100 NEXT
4110 CLOSE:LOCATE13,7:PRINT"-[";X$;"]- TO START"
4120 IFINKEY$<>CHR$(13)THEN4120
4130 CLS:SS$="":ONERRORGOTO0:RETURN'INTRO
4140 'SUB HOME
4150 IFSD>20THEN4170
4160 XX=10:YY=14:GOSUB4040:QP%=QP%OR3:GOTO4171
4170 XX=16:YY=20:GOSUB4040:QP%=QP%OR1
4171 IFAX=0THENFORX=0TO5:POKEMA+AY*W+X,2:NEXT
4172 IFAX=W-1THENFORX=W-6TOW-1:POKEMA+AY*W+X,2:NEXT
4173 IFAY=0THENFORY=0TO5:POKEMA+Y*W+AX,3:NEXT
4174 IFAY=H-1THENFORY=H-6TOH-1:POKEMA+Y*W+AX,3:NEXT
4180 POKEMA+AY*W+AX,69:FD=1:GOSUB550:GOSUB830
4190 RETURN'HOME
4200 'SUB WIN
4210 XX=22:YY=23:GOSUB4040:QP%=QP%OR4:GOSUB3040
4220 RETURN'WIN
10000 'SUB DIE
10010 HP=0:XX=0:YY=1:GOSUB4040:GOSUB3040
10020 RETURN'DIE
