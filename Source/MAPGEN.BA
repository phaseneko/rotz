10 'MAP GENERATOR FOR RotZ
20 CLEAR 300,60950!: MAXFILES=1: TA=60950!: IA=61430!: MA=61930!: CLS: SCREEN 0,0
30 '60950-61429: TILE DATA
40 '61430-61929: ITEM DATA
50 '61930-62330: MAP DATA
60 'READ PARAMETERS
70 '---------------
80 PRINT"{RotZ Map Generator}"
90 W=20: H=20
100 INPUT "Seed";SEED: TMP=RND(-SEED)
110 '--------------------
120 'GENERATE MAP CONTENT
130 '--------------------
140 BEEP: PRINT "GENERATING..."
141 FORY=0TOH-1:FORX=0TOW-1:POKEMA+Y*W+X,0:NEXTX:NEXTY
150 PRINT "Pouring water..." 'GENERATE WATER
160 X = INT(RND(SEED) * W)
170 Y = INT(RND(SEED) * H)
180 IF X>.5 THEN X=X+.25 ELSE X=X-.25
190 IF Y>.5 THEN Y=Y+.25 ELSE Y=Y-.25
200 FOR Y2=(Y-5)TO(Y+5): FOR X2=(X-5)TO(X+5)
210 IF X2>=0 AND Y2>=0 AND X2<W AND Y2<H AND (ABS(X2-X)+ABS(Y2-Y)<7) THEN POKE MA+Y2*W+X2,4
220 NEXT X2: NEXT Y2
230 IF RAND(SEED) < .2 THEN 150
240 'RANDOMIZE ROAD ROOT
250 CX = INT((RND(SEED) / 3 + .333) * W)
260 CY = INT((RND(SEED) / 3 + .333) * H)
270 PRINT "Placing roads and houses..."
280 DIM BRNCH%(48) 'X,Y,DIR
290 AI=0: BI=0 'AI:USED BI:RECORDED
300 GOTO 640
310 'SUB GEN_BRANCH(X,Y,DIR)
320 FST = 1 '1ST HALF
330 POKE MA+Y*W+X,1 'FILL ROAD
340 ON (DIR+1) GOTO 350,360,370,380
350 OX=1:OY=0:RD=2:GOTO390 '0-EAST
360 OX=0:OY=1:RD=3:GOTO390 '1-SOUTH
370 OX=-1:OY=0:RD=2:GOTO390 '2-WEST
380 OX=0:OY=-1:RD=3:GOTO390 '3-NORTH
390 OL=INT(RND(SEED)*4)+2
400 FOR I=0 TO OL
410  X=X+OX:Y=Y+OY
420  IF X>=0 AND Y>=0 AND X<W AND Y<H THEN MD=PEEK(MA+Y*W+X) ELSE RETURN
430  'RANDOMLY BREAK ROADS
440  RRD=RD: IF RND(SEED)<.2 THEN RRD = MD
450  POKE MA+Y*W+X,RRD
460 NEXT I
470 IF FST=0 THEN RETURN
480 IF RND(SEED) < .5 AND BI < 16 THEN BRNCH%(BI*3)=X:BRNCH%(BI*3+1)=Y:BRNCH%(BI*3+2)=((DIR+1) MOD 4):BI=BI+1
490 IF RND(SEED) < .5 AND BI < 16 THEN BRNCH%(BI*3)=X:BRNCH%(BI*3+1)=Y:BRNCH%(BI*3+2)=((DIR+3) MOD 4):BI=BI+1
500 'GEN DWELLINGS
510 IF RND(SEED) > .3 THEN 570
520 FOR Y2=(Y-2)TO(Y+2): FOR X2=(X-2)TO(X+2)
530 TMP = INT(RND(SEED)*15)+5: IF TMP>10 THEN TMP=5
540 IF X2>=0 AND Y2>=0 AND X2<W AND Y2<H AND RND(SEED) < .4 THEN POKE MA+Y2*W+X2,TMP
550 NEXT X2: NEXT Y2
560 'GEN BUSSINESS BUILDINGS
570 IF RND(SEED) > .6 THEN 620
580 FOR Y2=(Y-1)TO(Y+1): FOR X2=(X-1)TO(X+1)
590 TMP = INT(RND(SEED)*8)+11: IF TMP>16 THEN TMP=11
600 IF X2>=0 AND Y2>=0 AND X2<W AND Y2<H AND RND(SEED) < .3 THEN POKE MA+Y2*W+X2,TMP
610 NEXT X2: NEXT Y2
620 FST = 0: GOTO 390 '2ND HALF
630 RETURN 'GEN_BRANCH
640 X=CX:Y=CY:DIR=0:GOSUB310
650 X=CX:Y=CY:DIR=1:GOSUB310
660 X=CX:Y=CY:DIR=2:GOSUB310
670 X=CX:Y=CY:DIR=3:GOSUB310
680 IF BI=16 THEN GOTO 710 'GENERATE MORE BRANCHES
690 X=BRNCH%(AI*3):Y=BRNCH%(AI*3+1):DIR=BRNCH%(AI*3+2):GOSUB310
700 IF AI<BI-1 THEN AI=AI+1:GOTO680
710 'PLACE DUMPS
720 PRINT "Building dump sites..."
730 FOR I=0 TO 3
740  X = INT(RND(SEED) * (W-2))
750  Y = INT(RND(SEED) * (H-2))
760  IF PEEK(MA+Y*W+X)=0 AND RND(SEED)<.75 THEN POKE MA+Y*W+X,17:POKE MA+Y*W+X+1,17:POKE MA+(Y+1)*W+X,17:POKE MA+(Y+1)*W+X+1,17
770 NEXT I
780 GOTO 900
790 'SUB DRAW_MAP
800 BEEP: PRINT CHR$(27);"V";"Visualizing..."
810 TS$=".+-|/^SGD*T#MPOE$%X "
820 FOR Y=0 TO H-1
830 FOR X=0 TO W-1
840 LOCATE X+(Y/8 MOD 2)*20,(Y MOD 8)
850 PRINT MID$(TS$,PEEK(MA+Y*W+X)+1,1)
860 'STUB
870 NEXT X
880 NEXT Y
890 RETURN 'DRAW_MAP
900 GOSUB790 'DRAW MAP
910 LOCATE 0,7: PRINT STRING$(40," ")
920 LOCATE 0,7
930 INPUT "Map Name";FN$
940 IF FN$="" THEN 930
950 BEEP: LOCATE 0,7: PRINT STRING$(40," ")
960 PRINT "Map saved.";CHR$(27);"W"
970 BSAVE FN$,MA,400
