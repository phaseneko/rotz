10 CLEAR300,60850!:MAXFILES=2:EA=60850!:TA=60950!:IA=61430!:MA=61930!:CLS:SCREEN0,0
20 '60850-60949: ENEMY DATA
30 '60950-61429: TILE DATA
40 '61430-61929: ITEM DATA
50 '61930-62330: MAP DATA
60 PRINT "RotZ Data Maker Ver0.1"
70 PRINT "Ne^3 Games 2022"
80 PRINT STRING$(39,"-")
90 PRINT "Which data to build?"
100 INPUT "[I]tem [T]ile [E]nemy";SS$
110 IF SS$="I" THEN GOSUB150:END
120 IF SS$="T" THEN GOSUB280:END
130 IF SS$="E" THEN GOSUB450:END
140 GOTO 110
150 PRINT "Making item data..." 'MAKE ITEM DATA
160 OPEN "ITEM" FOR INPUT AS #1
170 OPEN "ITNAM.DO" FOR OUTPUT AS #2
180 INPUT# 1,N: IC=0
190 INPUT# 1,IX: IC=IC+1
200 INPUT# 1,SS$: PRINT IX;":";SS$: PRINT#2,SS$
210 FOR I=0 TO 7: INPUT#1,ZZ: POKE IA+IX*8+I,ZZ: NEXT
220 IF EOF(1) THEN 240
230 GOTO 190
240 CLOSE
250 PRINT IC;"items saved."
260 BSAVE "ITEM",IA,400:BEEP
270 RETURN 'MAKE ITEM DATA
280 PRINT "Making tile data..." 'MAKE TILE DATA
290 TX=0
300 OPEN "TILE" FOR INPUT AS #1
310 OPEN "TLNAM" FOR OUTPUT AS #2
320 INPUT#1,SS$
330 PRINT TX;":";SS$: PRINT#2,SS$
340 FOR I=0 TO 3: INPUT#1,ZZ: POKE TA+TX*24+I,ZZ: NEXT
350 'BEGIN LOAD TILE ITEM DROPS
360 ZZ=0: FOR I=0 TO 9: POKE TA+TX*24+4+I,0
370 INPUT#1,XX,YY: POKE TA+TX*24+4+ZZ*2,XX: POKE TA+TX*24+5+ZZ*2,YY
380 IF XX>0 THEN ZZ=ZZ+1:GOTO370
390 IF EOF(1) THEN410
400 TX=TX+1:GOTO320
410 CLOSE
420 PRINT TX+1;"tiles saved."
430 BSAVE "TILE",TA,480
440 RETURN 'MAKE TILE DATA
450 PRINT "Making enemy data..." 'SUB MAKE ENEMY DATA
460 EX=0
470 OPEN "ENEMY" FOR INPUT AS #1
480 OPEN "ENNAM" FOR OUTPUT AS #2
490 INPUT#1,SS$
500 PRINT EX;":";SS$:PRINT#2,SS$
510 FOR I=0 TO 9: INPUT#1,ZZ: POKE EA+EX*20+I,ZZ: NEXT
520 'BEGIN LOAD ENEMY ITEM DROPS
530 ZZ=0: FOR I=0 TO 4: POKE EA+EX*20+10+I,0
540 INPUT#1,XX,YY: POKE EA+EX*20+10+ZZ*2,XX: POKE EA+EX*20+11+ZZ*2,YY
550 IF XX>0 THEN ZZ=ZZ+1: GOTO540
560 IF EOF(1) THEN580
570 EX=EX+1: GOTO490
580 CLOSE
590 PRINT EX+1; "enemies saved."
600 BSAVE "ENEMY", EA, 100
610 RETURN 'MAKE ENEMY DATA
